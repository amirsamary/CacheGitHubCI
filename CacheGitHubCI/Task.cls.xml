<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Apple Mac OS X for x86-64) 2016.2 (Build 734U)" ts="2016-12-14 19:14:35">
<Class name="CacheGitHubCI.Task">
<Description>
Simple update task. Use CacheGitHubCI.Hook if you need more functionality.</Description>
<Super>%SYS.Task.Definition</Super>
<TimeChanged>64266,65078.957123</TimeChanged>
<TimeCreated>64232,70931.143606</TimeCreated>

<Parameter name="TaskName">
<Default>GitHubUpdateTask</Default>
</Parameter>

<Property name="GitHubURL">
<Description>
Repository URL, like https://github.com/intersystems-ru/Cache-MDX2JSON</Description>
<Type>%String</Type>
</Property>

<Property name="Username">
<Description><![CDATA[
GitHub user, who has access to repository. Optional for public repositories.<br>
Note, that with Username/Password, you can make up to 5,000 requests per hour. 
For unauthenticated requests, the rate limit allows to make up to 60 requests per hour. 
Unauthenticated requests are associated with an IP address.<br>
Required, if you want to create webhooks]]></Description>
<Type>%String</Type>
</Property>

<Property name="Password">
<Description>
GitHub password, corresponding to Username. Optional for public repositories.</Description>
<Type>%String</Type>
</Property>

<Property name="Branch">
<Type>%String</Type>
<InitialExpression>"master"</InitialExpression>
</Property>

<Property name="Namespace">
<Description>
Namespace, where to download and compile repository</Description>
<Type>%String</Type>
<InitialExpression>$Namespace</InitialExpression>
</Property>

<Property name="ConfigureActions">
<Description>
If no actions are defined for the hook representing this task and this flag is TRUE,
we will call ##class(CacheGitHubCGCI.ActionClassTemplate).SetupHook() to setup
default actions automatically.</Description>
<Type>%Boolean</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Property name="ActionClassName">
<Type>%String</Type>
</Property>

<Property name="KeepBrokenSourceOnError">
<Type>%Boolean</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Method name="OnTask">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Quit ..Update(..GitHubURL, ..Username, ..Password, ..Branch, ..Namespace, ..ConfigureActions, ..ActionClassName)
]]></Implementation>
</Method>

<Method name="Update">
<Description>
This method can be called directly by anyone that needs to update a namespace interactively</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pGitHubUrl:%String,pUserName:%String,pPassword:%String,pBranch:%String,pNamespace:%String,pConfigureActions:%Boolean,pActionClassName:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set tSC = $System.Status.OK()
	Try
	{		
		If '##class(%SYS.Namespace).Exists(pNamespace) 
		{
			Set tSC = $$$ERROR($$$NamespaceUnavailable,pNamespace)
			Quit
		}
	
		Set tOwner = $ZConvert($p(pGitHubUrl,"/",4),"L")
		Set tRepository = $ZConvert($p(pGitHubUrl,"/",5),"L")
		Set tNamespace = $ZConvert(pNamespace,"L")
		Set tHookId=tOwner_"||"_tRepository_"||"_tNamespace
		
		If ('##class(CacheGitHubCI.Hook).%ExistsId(tHookId)) 
		{
			Set hook = ##class(CacheGitHubCI.Hook).%New(tOwner,tRepository,pBranch,pUserName,pPassword,tNamespace,1)
			Quit:$System.Status.IsError(tSC)
		}
		Else
		{
			Set hook = ##class(CacheGitHubCI.Hook).%OpenId(tHookId)
			Set hook.Branch=pBranch
			Set hook.Username=pUserName
			Set hook.Password=pPassword
			Set hook.KeepBrokenSourceOnError=1
		}
		
		Set tSC = hook.%Save()
		
		If (pConfigureActions) && (hook.PostCompile.Type="") && (hook.PreCompile.Type="") && (hook.UnitTests.Type="") && (pActionClassName'="")
		{
			Set tSC = ##class(CacheGitHubCI.ActionClassTemplate).SetupHookObj(hook, pActionClassName)
			Quit:$System.Status.IsError(tSC)
		}
		
		Set tSC = ##class(CacheGitHubCI.Hook).Update(tOwner,tRepository,tNamespace)
	}
	Catch (oException)
	{
		Set tSC = oException.AsStatus()
	}
	
	Quit tSC
]]></Implementation>
</Method>

<Method name="ShellUpdate">
<Description>
This method is available to be called from csession INSTANCE -UCGCI </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pGitHubUrl:%String,pUserName:%String,pPassword:%String,pBranch:%String,pNamespace:%String,pConfigureActions:%Boolean,pActionClassName:%String</FormalSpec>
<Implementation><![CDATA[
	Set tSC = ..Update(pGitHubUrl, pUserName, pPassword, pBranch, pNamespace, pConfigureActions, pActionClassName)
	
	If $System.Status.IsError(tSC)
	{
		Do $System.Status.DisplayError(tSC)
		
		Set $zerror = $System.Status.GetErrorText(tSC)
		
		Do LOG^%ETN

		Do $System.Process.Terminate(0)
	}
	Else
	{
		Write !,"OK!"
		Do $System.Process.Terminate(1)
	}
]]></Implementation>
</Method>
</Class>
</Export>
